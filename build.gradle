// gradle task and plugin configuration
buildscript {
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.jfrog.bintray'

// global settings
group 'io.cloudwall'
version '0.2'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile 'ch.qos.logback:logback:0.5',
            'com.google.code.findbugs:jsr305:3.0.2',
            'com.google.guava:guava:22.0',
            'commons-io:commons-io:2.5',
            'org.javafp:parsecj:0.5',
            'org.jooq:jool:0.9.12',
            'org.slf4j:slf4j-api:1.7.25'
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
   	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	configurations = ['archives']
	pkg {
		repo = 'cloudwall-maven'
		name = 'cloudwall-graph'
		userOrg = 'io-cloudwall'
        desc = 'Cloud Wall Graph Algorithms'
        websiteUrl = 'https://github.com/cloudwall/cloudwall-graph'
        issueTrackerUrl = 'https://github.com/cloudwall/cloudwall-graph/issues'
		vcsUrl = 'https://github.com/cloudwall/cloudwall-graph.git'
        licenses = ['Apache-2.0']

        githubRepo = 'cloudwall/cloudwall-graph'
        githubReleaseNotesFile = 'README.md'
        
		version {
			name = '0.2'
			vcsTag = 'v0.2'
			attributes = ['gradle-plugin': 'io.cloudwall:cloudwall-graph:0.2']
		}
	}
}
